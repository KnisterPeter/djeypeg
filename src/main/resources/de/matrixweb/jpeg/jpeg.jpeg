JPEG :
  Rule+ #
;

Rule :
  Name WS* COLON Body SEMI WS*
;

Name :
  NameChar+
;
NameChar :
  !NameEndChar .
;
NameEndChar :
  WS | COLON
;

Body :
  BodyPart+ WS*
;
BodyPart :
  WS* ChoiceExpression
;

ChoiceExpression :
  SequenceExpression ChoiceExpressionPart*
;
ChoiceExpressionPart :
  '|' WS* SequenceExpression
;

SequenceExpression :
  SequenceExpressionPart1+
;
SequenceExpressionPart1 :
  PredicateExpression WS*
;

PredicateExpression :
    AndPredicateExpression
  | NotPredicateExpression
  | AtomicExpression
;

AndPredicateExpression :
  '&' RuleReferenceExpression
;

NotPredicateExpression :
  '!' RuleReferenceExpression
;

AtomicExpression :
  Terminal
  | OneOrMoreExpression
  | ZeroOrMoreExpression
  | OptionalExpression
  | AnyCharExpression
  | EndOfInputExpression
  | RuleReferenceExpression
;

OneOrMoreExpression :
  RuleReferenceExpression '+'
;

ZeroOrMoreExpression :
  RuleReferenceExpression '*'
;

OptionalExpression :
  RuleReferenceExpression '?'
;

AnyCharExpression :
  '.'
;

EndOfInputExpression :
  '#'
;

RuleReferenceExpression :
  RuleReferenceChar+
;
RuleReferenceChar :
  !RuleReferenceEnd .
;
RuleReferenceEnd :
 '+' | '*' | '?' | '|' | QUOTE | '&' | '!' | SEMI | WS
;

Terminal :
 QUOTE InTerminalChar* QUOTE
;
InTerminalChar :
    '\\' '\''
  | '\\' '\\'
  | !QUOTE .
;

WS : ' ' | '\n' | '\t' | '\r';
COLON : ':';
SEMI  : ';';
QUOTE : '\'';
