JPEG :
  Rule+ #
;

Rule :
  Name WS* COLON Body SEMI WS*
;

Name :
  NameChar+
;
NameChar :
  !NameEndChar .
;
NameEndChar :
  WS | COLON
;

Body :
  BodyPart+ WS*
;
BodyPart :
  WS* ChoiceExpression
;

ChoiceExpression :
  SequenceExpression ChoiceExpressionPart*
;
ChoiceExpressionPart :
  '|' WS* SequenceExpression
;

SequenceExpression :
  SequenceExpressionPart1+
;
SequenceExpressionPart1 :
  SequenceExpressionPart2 WS*
;
SequenceExpressionPart2 :
  Terminal
  | AndPredicateExpression
  | NotPredicateExpression
  | OneOrMoreExpression
  | ZeroOrMoreExpression
  | OptionalExpression
  | AnyCharExpression
  | EndOfInputExpression
  | RuleReferenceExpression
;

OneOrMoreExpression :
  RuleReferenceExpression '+'
;

ZeroOrMoreExpression :
  RuleReferenceExpression '*'
;

OptionalExpression :
  RuleReferenceExpression '?'
;

AnyCharExpression :
  '.'
;

EndOfInputExpression :
  '#'
;

RuleReferenceExpression :
  RuleReferenceChar+
;
RuleReferenceChar :
  !RuleReferenceEnd .
;
RuleReferenceEnd :
 '+' | '*' | '?' | '|' | QUOTE | '&' | '!' | SEMI | WS
;

AndPredicateExpression :
  '&' RuleReferenceExpression
;

NotPredicateExpression :
  '!' RuleReferenceExpression
;

Terminal :
 QUOTE InTerminalChar* QUOTE
;
InTerminalChar :
    '\\' '\''
  | '\\' '\\'
  | !QUOTE .
;

WS : ' ' | '\n' | '\t' | '\r';
COLON : ':';
SEMI  : ';';
QUOTE : '\'';
